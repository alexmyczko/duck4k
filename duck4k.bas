1 DIM PL(15):NN=1:SUNN=0:BRDN=0:BRDPOS=30:WLKP=35:DIM BRD$(3):DIM SUNA$(7):DIM SUNB$(7):DIM SUNC$(7)
2 SUNA$(0)="    / ":SUNA$(1)=" \    ":WLK1$="   _     "
3 SUNB$(0)="- oo  ":SUNB$(1)="  oo -":WLK2$="  ( )__  "
4 SUNC$(0)="    \ ":SUNC$(1)=" /    ":WLK3$=" (_____) "
5 WAK1$="  _()= ":WLAK1$=" =()_  ":BRD$(0)=" .--.--. "
6 WAK2$=" (__)  ":WLAK2$="  (__) ":BRD$(1)=" ---.--- "
7 BAK1$="  __()=":BLAK1$="=()__  ":BRD$(2)=" `--.--' "
8 BAK2$=" (__)  ":BLAK2$="  (__) ":BRD$(3)=" ---.--- "
9 LOCATE ,,1,7,8:PRINT"DOS/4GW Protected Mode Run-time  Version 1.97":N=.4:GOSUB 900
10 PRINT"Copyright (c) Rational Systems, Inc. 1990-1994":N=1.2:GOSUB 900
11 PRINT:PRINT"please wait generating sky...":N=1:GOSUB 900:PRINT"please wait calculating ducks...":N=1.3:GOSUB 900:PRINT"please wait rendering sun...":N=1.3:GOSUB 900
13 AA$="tarzeau / carrots":BB$="is proud to present":CC$="duck 4k"
14 SCREEN 1:SCREEN 0:WIDTH 80,25:CLS:RANDOMIZE TIMER
15 LOCATE 11,30,0:COLOR 8:PRINT AA$:N=.33:GOSUB 900:LOCATE 11,30:COLOR 7:PRINT AA$:N=.33:GOSUB 900:LOCATE 11,30:COLOR 15:PRINT AA$:GOSUB 900
17 LOCATE 13,29:COLOR 8:PRINT BB$:GOSUB 900:LOCATE 13,29:COLOR 7:PRINT BB$:GOSUB 900:LOCATE 13,29:COLOR 15:PRINT BB$:GOSUB 900
19 LOCATE 15,35:COLOR 8:PRINT CC$:GOSUB 900:LOCATE 15,35:COLOR 7:PRINT CC$:GOSUB 900:LOCATE 15,35:COLOR 15:PRINT CC$:N=1.33:GOSUB 900:CLS
21 WHILE (INKEY$="")
22 COLOR 14:LOCATE 3,55:PRINT SUNA$(SUNN):LOCATE ,55:PRINT SUNB$(SUNN):LOCATE ,55:PRINT SUNC$(SUNN)
23 SUNN=SUNN+.23:IF SUNN>1 THEN SUNN=0
24 COLOR 15
25 LOCATE 4,WLKP:PRINT WLK1$:LOCATE ,WLKP:PRINT WLK2$:LOCATE ,WLKP:PRINT WLK3$
26 IF NN MOD 2=1 THEN WLKP=WLKP-1
27 COLOR 7:LOCATE 8,BRDPOS:PRINT BRD$(BRDN)
28 BRDPOS=BRDPOS+1
29 BRDN=BRDN+1:IF BRDN>3 THEN BRDN=0
30 COLOR 6:LOCATE 12,NN:S$=WAK1$:GOSUB 105:LOCATE ,NN:PRINT WAK2$
31 LOCATE 12,75-NN:S$=WLAK1$:GOSUB 105:LOCATE ,75-NN:PRINT WLAK2$
32 N=.25:GOSUB 900
33 LOCATE 12,NN:S$=BAK1$:GOSUB 105:LOCATE ,NN:PRINT BAK2$
34 LOCATE 12,75-NN:S$=BLAK1$:GOSUB 105:LOCATE ,75-NN:PRINT BLAK2$
35 COLOR 1
36 LOCATE 14,1
37 PRINT STRING$(80,"~");
38 FOR I=15 TO 23
39 IF NOT RND*50>45 THEN GOTO 44
40 IF RND*10>5 THEN COLOR 1 ELSE COLOR 9
41 LOCATE I,1+(RND*79)
42 PRINT STRING$(3+(RND*10),"~");
44 NEXT
45 N=.15:GOSUB 900
46 NN=NN+1
47 IF NN>27 THEN WAK1$="  _()< ":BAK1$="  __()<":WAK2$=" (__)  ":BAK2$=" (__)  ":WLAK1$=" >()_  ":BLAK1$=">()__  ":WLAK2$="  (__) ":BLAK2$="  (__) "
51 IF NN>=10 AND NN<=14 THEN WAK1$="  _()< ":COLOR 15:LOCATE 12,NN+7:PRINT " wak":IF NN=14 THEN WAK1$="  _()= ":LOCATE 12,NN+7:PRINT "    "
56 IF NN>=16 AND NN<=20 THEN WLAK1$=" >()_  ":COLOR 15:LOCATE 12,75-NN-9:PRINT " wak wak ":IF NN=20 THEN WLAK1$=" =()_  ":LOCATE 12,75-NN-9:PRINT "         "
61 IF NN<>35 GOTO 89
62 COLOR 4:LOCATE 11,38:PRINT "*smack*":N=1:GOSUB 900:LOCATE 11,38:PRINT "       ":N=.5:GOSUB 900
64 LOCATE 11,41:PRINT "♥":GOSUB 900
65 LOCATE 10,39:PRINT "♥":GOSUB 900
66 LOCATE 9,40:PRINT "♥":GOSUB 900
67 LOCATE 11,41:PRINT " ":GOSUB 900
68 LOCATE 10,39:PRINT " ":GOSUB 900
69 LOCATE 9,40:PRINT " ":N=1:GOSUB 900:GOSUB 91
70 SCREEN 1:SCREEN 0:WIDTH 80,25:CLS :LOCATE 12,38,1,7,8:COLOR 9:PRINT "        ";
84 LOCATE ,,0
85 COLOR 7,0:CLS:SCREEN 1:SCREEN 0:WIDTH 80,25
86 PRINT"me@aiei.ch 1999":PRINT:PRINT "kebap for the people."
87 N=1:GOSUB 900:SYSTEM
89 WEND
90 COLOR 7,0

91 PL(0)=0:PL(1)=1:PL(2)=2:PL(3)=3:PL(4)=4:PL(5)=5:PL(6)=20:PL(7)=7:PL(8)=56:PL(9)=57:PL(10)=58:PL(11)=59:PL(12)=60:PL(13)=61:PL(14)=62:PL(15)=63
94 FOR J=0 TO 63
95 FOR I=0 TO 15
96 OUT &H3C7,PL(I):R=INP(&H3C9):G=INP(&H3C9):B=INP(&H3C9)
97 IF R>=1 THEN R=R-1
98 IF G>=1 THEN G=G-1
99 IF B>=1 THEN B=B-1
100 OUT &H3C8,PL(I):OUT &H3C9,R:OUT &H3C9,G:OUT &H3C9,B
101 GOSUB 910
102 NEXT I
103 NEXT J:SCREEN 1:SCREEN 0:RETURN

105 FOR I=1 TO LEN(S$)
106 IF (MID$(S$,I,1)<>"<") AND (MID$(S$,I,1)<>">") AND (MID$(S$,I,1)<>"=") THEN COLOR 6 ELSE COLOR 14
107 PRINT MID$(S$,I,1);
108 NEXT
109 COLOR 6:PRINT:RETURN

900 T=TIMER:WHILE (T+N>TIMER):WEND:RETURN

910 WAIT &H3DA,8:RETURN
